@ECHO OFF
GOTO START

:START

	REM LOAD VARS FROM CONFIG FILE.
	CALL GAME-DATA-CONFIG.BAT
	
	REM SET OTHER VARIABLES.
	SET SAINT_COINACH_DIR=%WORKSPACE%\SaintCoinach
	SET TOOLS_DIR=%WORKSPACE%\tools
	SET EXTRACTS_DIR=%WORKSPACE%\SaintCoinachExtracts
	SET CONFIGURATION=RELEASE

	REM CHECK IF SAINT COINACH DIR EXISTS.
	IF EXIST %SAINT_COINACH_DIR% (
		GOTO UPDATE
	) ELSE (
		GOTO INSTALL
	)

REM UPDATE SAINT COINACH.
:UPDATE
	cd %SAINT_COINACH_DIR%
	git checkout .
	git pull origin
	GOTO GENERATE_RAW
		
REM INSTALL SAINT COINACH.
:INSTALL
	git clone https://github.com/ufx/SaintCoinach.git
	cd %SAINT_COINACH_DIR%
	GOTO GENERATE_RAW
	
:GENERATE_RAW
	RMDIR /s /q %EXTRACTS_DIR%\
	CALL :GENERATE_RAW_LANG "English" "en"
	CALL :GENERATE_RAW_LANG "German" "de"
	CALL :GENERATE_RAW_LANG "French" "fr"
	CALL :GENERATE_RAW_LANG "Japanese" "ja"
	GOTO PARSE
	
REM GENERATE RAW DATA FILES.
:GENERATE_RAW_LANG
	CALL :BUILD %~2
	CD /D %SAINT_COINACH_DIR%\SaintCoinach.Cmd\bin\Release
	SaintCoinach.Cmd %FFXIV_DIR% "lang %~1" rawexd 
	MOVE ./Definitions ../Definitions
	for /F "tokens=*" %%x in ('dir /ad /b') do (set OUTPUT_DIR=%%x)
	MOVE ../Definitions ./Definitions
	xcopy %OUTPUT_DIR%\rawexd %EXTRACTS_DIR%\%~2\ /E
	EXIT /B 0
	
REM BUILD SAINT COINACH.
:BUILD
	CD %TOOLS_DIR%
	RMDIR /s /q %SAINT_COINACH_DIR%\SaintCoinach.Cmd\bin\Release
	RMDIR /s /q %EXTRACTS_DIR%\%~1
	NUGET.EXE RESTORE ../SAINTCOINACH/SAINTCOINACH.SLN
	cd %SAINT_COINACH_DIR%
	%DEVENV% .\SAINTCOINACH.SLN /Rebuild %CONFIGURATION%
	EXIT /B 0
REM CREATE PROPERTY FILES FROM EXTRACTS.

:PARSE
	CD /D %WORKSPACE%/scripts
	python transform-saint-coinach.py

pause